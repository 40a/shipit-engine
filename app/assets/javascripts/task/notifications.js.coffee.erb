class @Notifications
  IMAGES =
    success: '<%= image_path "deploy_success.jpg" %>'
    failed: '<%= image_path "deploy_failed.jpg" %>'
    error: '<%= image_path "deploy_error.jpg" %>'

  @init: (outputStream) ->
    if repoName = $('[data-repo-full-name]').attr('data-repo-full-name')
      outputStream.addEventListener('status', new this(repoName).statusUpdated)

  constructor: (@repoName) ->

  statusUpdated: (status) =>
    return unless status of IMAGES
    return unless $.notifyCheck() == $.NOTIFY_ALLOWED
    $.notify(IMAGES[status], @repoName, @message(status))

  message: (status) ->
    deployShortSha = $('.short-sha').text()
    switch status
      when 'success'
        "Your deploy of #{deployShortSha} was successful!"
      when 'failed'
        "Your deploy of #{deployShortSha} failed."
      when 'error'
        "Error while deploying #{deployShortSha}."
      else
        "Your deploy of #{deployShortSha} ended with status: #{status}"
