$(document).on 'click', '.deploy-abort .deploy-action', (event) ->
  event.preventDefault()
  $target = $(event.currentTarget)
  return if $target.hasClass('pending')
  $target.addClass('pending')
  enable = -> $target.removeClass('pending')
  $.post($target.attr('href')).done(-> setTimeout(enable, 3000))


class @ChunkPoller
  FORMATTERS = []
  INTERVAL = 1000
  MAX_RETRIES = 15
  STICKY_SCROLL_TOLERENCE = 200
  NOTIFICATION_IMAGES =
    success: '<%= image_path "deploy_success.jpg" %>'
    failed: '<%= image_path "deploy_failed.jpg" %>'
    error: '<%= image_path "deploy_error.jpg" %>'

  @init: ->
    $output = $('code')
    pollUrl = $output.data('next-chunks-url')
    poller = new ChunkPoller($('body'), pollUrl)

    output = $output.text()
    $output.html('')
    poller.appendChunks([text: output])
    poller.start() if pollUrl

  @appendFormatter: (formatter) ->
    FORMATTERS.push(formatter)

  @prependFormatter: (formatter) ->
    FORMATTERS.unshift(formatter)

  constructor: ($body, @pollUrl) ->
    @$status = $body.find('[data-deploy-status]')
    @$code = $body.find('code')
    @$body = $body
    @$window = $(window)
    @initialScroll = true
    @deployShortSha = $body.find('.short-sha').text()

  formatChunks: (chunk) ->
    for formatter in FORMATTERS
      chunk = formatter(chunk) || chunk
    chunk

  poll: =>
    jQuery.ajax @pollUrl,
      success: @update
      error: @error

  error: (response) =>
    (@retries ||= 0)
    @start() if 600 > response.status > 500 && (@retries += 1) < MAX_RETRIES

  update: (response) =>
    @retries = 0
    @pollUrl = response.url
    @restoreBrowserScroll =>
      @appendChunks(response.chunks)
    @updateDeployStatus(response.task)
    if @deployIsFinished(response.task)
      @notifyFinishedDeploy(response.task)
    else
      @start()

  restoreBrowserScroll: (callback) ->
    wasScrolledToBottom = @isScrolledToBottom()
    callback()
    if wasScrolledToBottom
      @$window.scrollTop(@codeBottomPosition() - @$window.height() + 50)

  isScrolledToBottom: ->
    if @initialScroll
      @initialScroll = (window.scrollY == 0)
      true
    else
      @viewportBottomPosition() >= @codeBottomPosition() - STICKY_SCROLL_TOLERENCE and \
      @viewportBottomPosition() < @codeBottomPosition() + STICKY_SCROLL_TOLERENCE

  viewportBottomPosition: ->
    window.scrollY + @$window.height()

  codeBottomPosition: ->
    @$code.position().top + @$code.height()

  start: ->
    setTimeout(@poll, INTERVAL)

  updateDeployStatus: (deploy) ->
    @$status.attr('data-deploy-status', deploy.status)

  deployIsFinished: (deploy) ->
    @$status.attr('data-deploy-status') not in ['pending', 'running']

  notifyFinishedDeploy: (deploy) ->
    return unless $.notifyCheck() == $.NOTIFY_ALLOWED
    $.notify(
      @notificationURL(deploy),
      @notificationTitle(deploy),
      @notificationMessage(deploy)
    )

  notificationURL: (deploy) ->
    NOTIFICATION_IMAGES[deploy.status]

  notificationTitle: (deploy) ->
    $('.repo-name').attr('data-repo-full-name')

  notificationMessage: (deploy) ->
    switch deploy.status
      when 'success'
        'Your deploy of ' + @deployShortSha + ' was successful!'
      when 'failed'
        'Your deploy of ' + @deployShortSha + ' failed.'
      when 'error'
        'Error while deploying ' + @deployShortSha + ' .'
      else
        'Your deploy of ' + @deployShortSha + ' ended with status: ' + deploy.status

  appendChunks: (chunks) ->
    return unless chunks.length

    text = chunks.map((c) -> c.text).join('')
    @$code.append(@formatChunks(text))
